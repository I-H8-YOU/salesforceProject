public with sharing class AICaseController {
    @AuraEnabled
    public static AICaseResult generateCaseDetails(String customerName, String issueDescription) {
        String firstName = customerName.length() > 1 ? customerName.substring(1) : '';
        String lastName = customerName.substring(0, 1);

        Contact c = [
            SELECT Id, AccountId, Account.Name 
            FROM Contact 
            WHERE FirstName = :firstName AND LastName = :lastName 
            LIMIT 1
        ];

        List<Asset> assets = [
            SELECT Name 
            FROM Asset 
            WHERE AccountId = :c.AccountId
        ];
        List<String> assetNames = new List<String>();
        for (Asset a : assets) {
            assetNames.add(a.Name);
        }
        String assetList = String.join(assetNames, '\n');

        Map<String, ConnectApi.WrappedValue> inputParams = new Map<String, ConnectApi.WrappedValue>{
            'Input:customer_name' => wrap(customerName),
            'Input:description' => wrap(issueDescription),
            'Input:account_name' => wrap(c.Account.Name),
            'Input:asset_list' => wrap(assetList)
        };

        // Prompt í˜¸ì¶œ íŒŒë¼ë¯¸í„° ì„¤ì •
        ConnectApi.EinsteinPromptTemplateGenerationsInput input = new ConnectApi.EinsteinPromptTemplateGenerationsInput();
        input.inputParams = inputParams;
        input.isPreview = false;
        input.additionalConfig = new ConnectApi.EinsteinLlmAdditionalConfigInput();
        input.additionalConfig.applicationName = 'PromptBuilderPreview';

        try {
            // Prompt í˜¸ì¶œ
            ConnectApi.EinsteinPromptTemplateGenerationsRepresentation response =
                ConnectApi.EinsteinLLM.generateMessagesForPromptTemplate('Case_Generation_With_Prompt', input);

            if (response.generations.isEmpty()) {
                throw new AuraHandledException('âš ï¸ LLM ì‘ë‹µì´ ë¹„ì–´ ìˆìŠµë‹ˆë‹¤. Prompt ì„¤ì • ë˜ëŠ” ì…ë ¥ê°’ì„ í™•ì¸í•˜ì„¸ìš”.');
            }

            String json = response.generations[0].text;
            System.debug('== LLM JSON ì‘ë‹µ ==\n' + json);

            Map<String, Object> parsed = (Map<String, Object>) System.JSON.deserializeUntyped(json);

            AICaseResult result = new AICaseResult();
            result.contactId = c.Id;
            result.accountId = c.AccountId;
            result.subject = (String) parsed.get('subject');
            result.description = (String) parsed.get('description');
            result.assetName = (String) parsed.get('asset_name');
            return result;
        } catch (Exception e) {
            System.debug('ğŸ”¥ AI ì¼€ì´ìŠ¤ ìƒì„± ì¤‘ ì˜¤ë¥˜: ' + e.getMessage());
            throw new AuraHandledException('AI ë¶„ì„ ì¤‘ ë¬¸ì œê°€ ë°œìƒí–ˆìŠµë‹ˆë‹¤. ê´€ë¦¬ìì—ê²Œ ë¬¸ì˜í•´ì£¼ì„¸ìš”.');
        }
    }

    @AuraEnabled
    public static List<IdNameWrapper> getAssetsByAccountId(Id accountId) {
        List<Asset> assets = [SELECT Id, Name FROM Asset WHERE AccountId = :accountId];
        List<IdNameWrapper> result = new List<IdNameWrapper>();
        for (Asset a : assets) {
            result.add(new IdNameWrapper(a.Id, a.Name));
        }
        return result;
    }

    private static ConnectApi.WrappedValue wrap(String value) {
        ConnectApi.WrappedValue w = new ConnectApi.WrappedValue();
        w.value = value;
        return w;
    }

    public class AICaseResult {
        @AuraEnabled public String contactId;
        @AuraEnabled public String accountId;
        @AuraEnabled public String subject;
        @AuraEnabled public String description;
        @AuraEnabled public String assetName;
    }

    public class IdNameWrapper {
        @AuraEnabled public String id;
        @AuraEnabled public String name;
        public IdNameWrapper(Id id, String name) {
            this.id = id;
            this.name = name;
        }
    }
}